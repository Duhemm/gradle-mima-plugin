plugins {
    id 'java-gradle-plugin'
    id 'scala'
    id 'com.github.maiflai.scalatest' version "0.31"
    // Publishing to maven
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.0.0'
}

group 'me.jeffshaw.mima'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    def mima = "1.1.1"
    def scalaLangVersion = '2.12'
    implementation "com.typesafe:mima-core_$scalaLangVersion:$mima"
    api 'org.scala-lang:scala-compiler:2.12.16'
    testImplementation "org.scalatest:scalatest_$scalaLangVersion:3.2.14"
    implementation gradleApi()
    testRuntimeOnly 'com.vladsch.flexmark:flexmark-all:0.62.2'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

gradlePlugin {
    plugins {
        mima {
            id = "me.jeffshaw.mima.mima-gradle-plugin"
            displayName = "Scala mima gradle plugin"
            description = 'a plugin to run scala mima on your library to check backwards compatibility'
            implementationClass = "the.flowering.branches.mima.MimaPlugin"
        }
    }
}

/*
 * This Source Code Form is subject to the terms of the Mozilla Public License,
 *  v. 2.0. If a copy of the MPL was not distributed with this file,
 *  You can obtain one at https://mozilla.org/MPL/2.0/.
 */

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
    website = "https://github.com/shawjef3/gradle-mima-plugin"
    vcsUrl = "https://github.com/shawjef3/gradle-mima-plugin"
    tags = ["mima", "scala", "binarycompatibility", "compatibility"]
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}